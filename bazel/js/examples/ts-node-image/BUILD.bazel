load("//bazel/js:exports.bzl", "js_binary", "js_image_layer", "ts_project")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")

# -----------------------------------------------------------------------------
# Platform generic can move out to workspace root
# -----------------------------------------------------------------------------

platform(
    name = "linux_amd64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform(
    name = "linux_arm64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:arm64",
    ],
)

# -----------------------------------------------------------------------------
# Source
# -----------------------------------------------------------------------------

ts_project(
    name = "project",
    srcs = ["index.ts"],
)

js_binary(
    name = "bin",
    data = [":project"],
    entry_point = "index.js",
)

# -----------------------------------------------------------------------------
# Image
# -----------------------------------------------------------------------------

js_image_layer(
    name = "layers",
    binary = ":bin",
    root = "/app",
    visibility = ["//visiblity:__pkg__"],
)

# oci_image isn't setting arch in image config section
# https://github.com/bazel-contrib/rules_oci/issues/117
oci_image(
    name = "image",
    base = "@debian",
    # This is going to be /{root of js_image_layer}/{package_name()}/{name of js_binary}
    cmd = ["/app/bazel/js/examples/ts-node-image/bin"],
    entrypoint = ["bash"],
    tars = [
        ":layers",
    ],
    visibility = ["//visibility:public"],
)

platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": ":linux_arm64",
        "@platforms//cpu:x86_64": ":linux_amd64",
    }),
)

# bazel build //bazel/js/examples/ts-node-image:tarball
# podman load --input $(bazel cquery --output=files //bazel/js/examples/ts-node-image:tarball)
# podman run --rm example:latest
oci_tarball(
    name = "tarball",
    image = ":image",
    repo_tags = ["example:latest"],
)
